reset
##########################################################
# Gnuplot script file for plotting data in file
##################### terminal settings ##################
set size 1.0,1.0

##################### plot ###############################

set terminal postscript eps 20 color size 5.0,2.6
set termoption enhanced
set encoding utf8

set autoscale xy
set autoscale y2

infile_uniform_normal = 'gnuplot_data/temp.llt_get_cuttime_UNIFORM_NORMAL.data'
infile_uniform_vulnerable = 'gnuplot_data/temp.llt_get_cuttime_UNIFORM_VULNERABLE.data'
infile_skewed_normal = 'gnuplot_data/temp.llt_get_cuttime_SKEWED_NORMAL.data'
infile_skewed_vulnerable = 'gnuplot_data/temp.llt_get_cuttime_SKEWED_VULNERABLE.data'

outfile = 'Figure_14_effect_of_classification_errors.eps'
set output outfile

set multiplot layout 2,2
set style data histograms
set style histogram rowstacked
set style fill solid
set boxwidth 1
set xrange [-4:60]
set yrange [0:100]

set label 1 "PostgreSQL w/ vDriver" at screen 0.49,0.92 center
set label 2 "cut delay (sec)" at screen 0.49,0.08 center
set label 3 "# of segments" at screen 0.04,0.5 center rotate by 90

unset xtics

set lmargin 8
set rmargin 0
set tmargin 3
set bmargin 0
set ytics ("0" 0, "50" 50, "100" 100)
set label 4 "uniform, {/Symbol d}\_{LLT}(normal)" at screen 0.15,0.79 left
plot \
	infile_uniform_normal using 4 notitle lc rgb "web-green", \
	infile_uniform_normal using 3 notitle lc rgb "blue", \
	infile_uniform_normal using 2 notitle lc rgb "red"
unset lmargin
unset rmargin
unset tmargin
unset bmargin
unset label 1
unset label 2
unset label 3

set lmargin 0
set rmargin 8
set tmargin 3
set bmargin 0
unset ytics
set label 4 "skewed, {/Symbol d}\_{LLT}(normal)" at screen 0.85,0.79 right
set key at screen 0.86,0.74 right
plot \
	infile_skewed_normal using 4 lc rgb "web-green" title 'LLT', \
	infile_skewed_normal using 3 lc rgb "blue" title 'COLD', \
	infile_skewed_normal using 2 lc rgb "red" title 'HOT'
unset lmargin
unset rmargin
unset tmargin
unset bmargin

set lmargin 8
set rmargin 0
set tmargin 0
set bmargin 4
set ytics ("0" 0, "50" 50, "100" 100)
set xtics ("0" 0, "5" 10, "10" 20, "15" 30, "20" 40, "25" 50)
set label 4 "uniform, {/Symbol d}\_{LLT}(vulnerable)" at screen 0.15,0.45 left
plot \
	infile_uniform_vulnerable using 4 notitle lc rgb "web-green", \
	infile_uniform_vulnerable using 3 notitle lc rgb "blue", \
	infile_uniform_vulnerable using 2 notitle lc rgb "red"
unset lmargin
unset rmargin
unset tmargin
unset bmargin

set lmargin 0
set rmargin 8
set tmargin 0
set bmargin 4
set xtics ("0" 0, "5" 10, "10" 20, "15" 30, "20" 40, "25" 50, "30" 60)
unset ytics
set label 4 "skewed, {/Symbol d}\_{LLT}(vulnerable)" at screen 0.85,0.45 right
plot \
	infile_skewed_vulnerable using 4 notitle lc rgb "web-green", \
	infile_skewed_vulnerable using 3 notitle lc rgb "blue", \
	infile_skewed_vulnerable using 2 notitle lc rgb "red"
unset lmargin
unset rmargin
unset tmargin
unset bmargin

unset multiplot
